Given:
  V E U (* U is a set of size = size of clique we want to find *)

Find: 
  C (* clique *)
  F (* 1-to-1 mapping between elements of C and U *)

Satisfying:
  (* if F relates a vertex x of the graph to an element of y of the set U 
     (of size of our clique), then x is in the clique *)
  (forall x in V (
    forall y in U (
      F(x y) -> C(x)
    )
  ) )
  &
  (* the next to conjuncts ensure that F is a 1-to-1 correspondence between 
     C (the clique) and set U *)
  (forall x in V (
    C(x) -> exists y in U ( (F(x y) ) & 
	            (forall z in U ( F(x z) -> y=z ) )
  ) ) )
  &
  (forall y in U (
     exists x in V ( (F(x y) & C(x)) &
               (forall z in V ( (F(z y) & C(z)) -> x=z ) )
  ) ) )
  &
  (* all vertices in the clique must be connected by an edge *)
  (forall x in V (
    forall y in V ( x=y |
      ((C(x) & C(y)) -> (E(x y) | E(y x)))
    )
  ) )
